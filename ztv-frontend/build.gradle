plugins {
    id "base"
    id "com.github.node-gradle.node" version "1.3.0"
}

node {
    // Version of node to use.
    version = "10.16.0"
    npmVersion = "6.9.0"
    download = true
}

task packageNpmApp(type: Zip) {
    dependsOn npm_run_build
    archiveName "ztv-frontend.jar"
    //extension "jar"
    destinationDir file("${projectDir}/dist_packageFrontend")
    from("dist") {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into "static"
    }
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file "package.json"
    inputs.file "package-lock.json"

    outputs.dir "dist"
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

assemble.dependsOn(packageNpmApp)

clean {
    delete packageNpmApp.archivePath
}

